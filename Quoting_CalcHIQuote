USE [AirTrak]
GO
/****** Object:  StoredProcedure [dbo].[Quoting_CalcHIQuote]    Script Date: 2/27/2020 10:37:32 AM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER procedure [dbo].[Quoting_CalcHIQuote] 

@CustID INT, @SummaryID INT

AS
BEGIN TRY
BEGIN


/**********************************************************************************OCEAN CHARGE**********************************************************************************/
			BEGIN

								DECLARE @OceanFee FLOAT 
								SET		@OceanFee =	ISNULL(			(
										
																				SELECT ISNULL(HORAD.OceanCharge,0.00) * CASE WHEN CONVERT(FLOAT,CONVERT(FLOAT,TotWeight) / CONVERT(FLOAT,CSAF.TotCubicFeet)) > 45.00 THEN CONVERT(FLOAT,TotWeight) / 45.00
																															 ELSE CSAF.TotCubicFeet 
																															 END --45LB Rule
																				FROM		Quoting_SWESummary CSAF
																				CROSS APPLY (
																								SELECT	TOP 1 DestZipCode
																								FROM	Quoting_SWEDetails SD
																								WHERE	SD.QuoteNo = CSAF.SummaryID 
																								and		SD.CustID = CSAF.CustID
																							) x
																				INNER JOIN  Quoting_HiOceanRatesandDiscounts HORAD ON HORAD.CustID = CSAF.CustID AND HORAD.ZipCode = X.DestZipCode
																				WHERE	CSAF.custid = @CustID 
																				AND		CSAF.SummaryID = @SummaryID
																				AND		CSAF.OceanFee = 0.00
																				AND		CAST(GETDATE() AS DATE) >= HORAD.Effective 
																				AND		CAST(GETDATE() AS DATE) < COALESCE(HORAD.Terminated, GETDATE())
							
																	)
													,0.00)
								

								--Apply Minimums. If applicable then discount is 0%
								UPDATE		CSAF 
								SET			OceanFee = ISNULL(HORAD.OceanMin,0.00)												
								FROM		Quoting_SWESummary CSAF
								CROSS APPLY (
												SELECT	TOP 1 DestZipCode
												FROM	Quoting_SWEDetails SD
												WHERE	SD.QuoteNo = CSAF.SummaryID 
												and		SD.CustID = CSAF.CustID
											) X
								INNER JOIN  Quoting_HiOceanRatesandDiscounts HORAD on HORAD.CustID = CSAF.CustID AND HORAD.ZipCode = X.DestZipCode
								WHERE	CSAF.custid = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.OceanFee = 0.00
								AND		@OceanFee * (1 - ISNULL(HORAD.OceanDiscount,0.00)) <= HORAD.OceanMin
								AND		HORAD.OceanMin IS NOT NULL
								AND		CAST(GETDATE() AS DATE) >= HORAD.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(HORAD.Terminated, GETDATE())

								--Apply Ocean Fee for all shipments beyond minimums
								UPDATE		CSAF 
								SET			OceanFee = ISNULL(@OceanFee,0.00)							
								FROM		Quoting_SWESummary CSAF
								CROSS APPLY (
												SELECT	TOP 1 DestZipCode
												FROM	Quoting_SWEDetails SD
												WHERE	SD.QuoteNo = CSAF.SummaryID 
												and		SD.CustID = CSAF.CustID
											) X
								INNER JOIN  Quoting_HiOceanRatesandDiscounts HORAD on HORAD.CustID = CSAF.CustID AND HORAD.ZipCode = X.DestZipCode
								WHERE	CSAF.custid = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.OceanFee = 0.00
								AND		CAST(GETDATE() AS DATE) >= HORAD.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(HORAD.Terminated, GETDATE())

								--Ocean Disc Perc
								UPDATE		CSAF 
								SET			OceanDiscPerc = ISNULL(HORAD.OceanDiscount,0.00)
								FROM		Quoting_SWESummary CSAF
								CROSS APPLY (
												SELECT	TOP 1 DestZipCode
												FROM	Quoting_SWEDetails SD
												WHERE	SD.QuoteNo = CSAF.SummaryID 
												AND		SD.CustID = CSAF.CustID
											) X
								INNER JOIN  Quoting_HiOceanRatesandDiscounts HORAD ON HORAD.CustID = CSAF.CustID AND HORAD.ZipCode = X.DestZipCode
								WHERE	CSAF.Custid = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		@OceanFee * (1 - ISNULL(HORAD.OceanDiscount,0.00)) > HORAD.OceanMin
								AND		CSAF.OceanDiscPerc = 0.00
								AND		CAST(GETDATE() AS DATE) >= HORAD.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(HORAD.Terminated, GETDATE())


								--Ocean FSC Perc
								UPDATE		CSAF 
								SET			OceanFSCPerc = ISNULL(Y.FSCPerc,0.00)
								FROM		Quoting_SWESummary CSAF
								CROSS APPLY (
												SELECT	TOP 1 DestState
												FROM	Quoting_SWEDetails SD
												WHERE	SD.QuoteNo = CSAF.SummaryID 
												AND		SD.CustID = CSAF.CustID
											) X
								CROSS JOIN  (
												SELECT	FSCPerc, DestState, Effective, Terminated 
												FROM	[Quoting_FSC]
												WHERE	[FeeSubTypeID] = 13
												AND		DestState = 'HI'
											) Y
								WHERE	CSAF.custid = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.OceanFSCPerc = 0.00		
								AND		Y.DestState = X.DestState
								AND		CAST(GETDATE() AS DATE) >= Y.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(Y.Terminated, GETDATE())

								--Ocean FSC Fee
								UPDATE		CSAF 
								SET			OceanFSCFee = (OceanFee * ( 1-[OceanDiscPerc]) ) * OceanFSCPerc
								FROM		Quoting_SWESummary CSAF
								WHERE		CSAF.custid = @CustID 
								AND			CSAF.SummaryID = @SummaryID
								AND			CSAF.TotOceanFee = 0.00

								--Ocean Total
								UPDATE		CSAF 
								SET			TotOceanFee = ( [OceanFee] * ( 1 - [OceanDiscPerc]) ) +  OceanFSCFee
								FROM		Quoting_SWESummary CSAF
								WHERE		CSAF.custid = @CustID 
								AND			CSAF.SummaryID = @SummaryID
								AND			CSAF.TotOceanFee = 0.00


			END


/**********************************************************************************DESTINATION DELIVERY CHARGE**********************************************************************************/

			BEGIN
							
								--Default Quote to WC if Quote is for a destination that is WC Only
								UPDATE		SD 
								SET			WillCall = 'Y'
								FROM		Quoting_SWEDetails SD
								INNER JOIN	Quoting_HiDestDelivRates DD ON DD.CustID = SD.CustID AND DD.ZipCode = SD.DestZipCode AND DD.Zone = 'WC'
								WHERE		SD.CustID = @CustID
								AND			SD.QuoteNo = @SummaryID


								--Apply rate per cubic feet, spot rate & spot rate PLUS per cubic feet

								IF OBJECT_ID ('TEMPDB..#DeliveryRate') IS NOT NULL  DROP TABLE #DeliveryRate
								SELECT		CSAF.SummaryID, CSAF.CustID, TotCubicFeet, CASE WHEN TotCubicFeet BETWEEN 0 AND 99 THEN ISNULL([DestDeliv_0-99],0.00)
																							WHEN TotCubicFeet BETWEEN 100 AND 199 THEN ISNULL([DestDeliv_100-199],0.00)
																							WHEN TotCubicFeet BETWEEN 200 AND 299 THEN ISNULL([DestDeliv_200-299],0.00)
																							WHEN TotCubicFeet BETWEEN 300 AND 399 THEN ISNULL([DestDeliv_300-399],0.00)
																							WHEN TotCubicFeet >= 400 THEN  ISNULL([DestDeliv_400+],0.00)
																							END * CONVERT(DECIMAL(6,2),TotCubicFeet) DestDelivery, 
											ISNULL([DestDelivHZMT],0.00) [DestDelivHZMT], CONVERT(DECIMAL(6,2),TotCubicFeet) * [DestDelivWC] AS [DestDelivWC], [DestDelivWCMin], [DestDelivMin], [DestDelivMax], 
											ISNULL([ContSpotRate],0.00) + (CONVERT(DECIMAL(6,2),TotCubicFeet) * ISNULL([ContSpotRatePLUSPerCuFt],0.00)) AS [ContSpotRate]
								INTO		#DeliveryRate
								FROM		Quoting_SWESummary CSAF
								CROSS APPLY (
												SELECT		TOP 1 Island, SD.DestCity City, DestZipCode, IsHazmat, WillCall 
												FROM		Quoting_SWEDetails SD
												INNER JOIN (
																SELECT	DISTINCT CustID, ZipCode, Island, City  
																FROM	Quoting_HiDestDelivRates
																WHERE	CustID = @CustID 
															) X		ON X.custid = SD.CustID AND X.ZipCode = SD.DestZipCode AND X.City = SD.DestCity
												WHERE   SD.CustID = CSAF.CustID 
												AND		SD.QuoteNo = CSAF.SummaryID
											) X
								CROSS APPLY (
													SELECT  [DestDeliv_0-99], [DestDeliv_100-199], [DestDeliv_200-299], [DestDeliv_300-399], [DestDeliv_400+], [DestDelivHZMT], ISNULL([DestDelivWC],0.00) [DestDelivWC], [DestDelivWCMin], [DestDelivMin], [DestDelivMax], [ContSpotRate], 
															[ContSpotRatePLUSPerCuFt], [Effective], [Terminated] 
													FROM	Quoting_HiDestDelivRates DD 
													WHERE  DD.CustID = CSAF.CustID 
													AND	DD.ZipCode = X.DestZipCode
													AND	DD.Island = X.Island
													AND DD.City = X.City 
											) Y
								WHERE	CSAF.CustID = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.DestDelivFee = 0.00
								AND		CSAF.HZMTDestDelivFee = 0.00
								AND		CAST(GETDATE() AS DATE) >= Y.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(Y.Terminated, GETDATE())

								--Apply Container Spot Rate (if applicable)

								DECLARE		@TotCubicFeet INT = (SELECT TotCubicFeet FROM #DeliveryRate)

								IF @TotCubicFeet >= 400

								BEGIN
											--Apply Dest Deliv Fee
											UPDATE			CSAF 
											SET				DestDelivFee = ISNULL(
																					CASE WHEN DestDelivery + ContSpotRate <= DestDelivMin AND DestDelivMin IS NOT NULL THEN DestDelivMin
																						 WHEN DestDelivery + ContSpotRate > DestDelivMin AND DestDelivMin IS NOT NULL AND DestDelivMax IS NOT NULL AND DestDelivery + ContSpotRate > DestDelivMax THEN DestDelivMax
																						 WHEN DestDelivery + ContSpotRate > DestDelivMin AND DestDelivMin IS NOT NULL THEN DestDelivery + ContSpotRate
																						 END
																			,0.00), 
															HZMTDestDelivFee = CASE IsHazmat WHEN 'Y' THEN DestDelivHZMT ELSE 0.00 END
											FROM		Quoting_SWESummary CSAF
											INNER JOIN	#DeliveryRate X ON X.CustID = CSAF.CustID AND X.SummaryID = CSAF.SummaryID 
											CROSS APPLY (
															SELECT  TOP 1 IsHazmat, WillCall
															FROM	Quoting_SWEDetails SD
															WHERE	SD.QuoteNo = CSAF.SummaryID 
															AND		SD.CustID = CSAF.CustID
															AND		SD.WillCall = 'N'
														) Y
											WHERE	CSAF.CustID = @CustID 
											AND		CSAF.SummaryID = @SummaryID
											AND		CSAF.HZMTDestDelivFee = 0.00
											AND		CSAF.DestDelivFee = 0.00

											--Apply Will Call Fee - No Dest Deliv Applied when shipment is WC
											UPDATE			CSAF 
											SET				WillCallFee = ISNULL(
																				  CASE WHEN DestDelivWC <= ISNULL(DestDelivWCMin,0.00) THEN ISNULL(DestDelivWCMin,0.00)
																					   WHEN DestDelivWC > ISNULL(DestDelivWCMin,0.00) THEN DestDelivWC
																					   END
																			,0.00), 
															HZMTDestDelivFee = DestDelivHZMT --Not applied on WC shipments
											FROM		Quoting_SWESummary CSAF
											INNER JOIN	#DeliveryRate X ON X.CustID = CSAF.CustID AND X.SummaryID = CSAF.SummaryID 
											CROSS APPLY (
															SELECT  TOP 1 IsHazmat, WillCall
															FROM	Quoting_SWEDetails SD
															WHERE	SD.QuoteNo = CSAF.SummaryID 
															AND		SD.CustID = CSAF.CustID
															AND		SD.WillCall = 'Y'
														) Y
											WHERE	CSAF.CustID = @CustID 
											AND		CSAF.SummaryID = @SummaryID
											AND		CSAF.HZMTDestDelivFee = 0.00
											AND		CSAF.DestDelivFee = 0.00

								END


								ELSE

											--Apply Dest Delivery Rate OR Minimums (if applicable)
											UPDATE			CSAF 
											SET				DestDelivFee = ISNULL(
																						CASE WHEN DestDelivery <= DestDelivMin AND DestDelivMin IS NOT NULL THEN DestDelivMin
																							 WHEN DestDelivery > DestDelivMin AND DestDelivMin IS NOT NULL THEN DestDelivery
																							 WHEN DestDelivery > DestDelivMin AND DestDelivMin IS NOT NULL AND DestDelivMax IS NOT NULL AND DestDelivery > DestDelivMax THEN DestDelivMax
																							 END
																			,0.00), 
															HZMTDestDelivFee = CASE IsHazmat WHEN 'Y' THEN DestDelivHZMT ELSE 0.00 END
											FROM		Quoting_SWESummary csaf
											INNER JOIN  #DeliveryRate X ON X.CustID = CSAF.CustID AND X.SummaryID = CSAF.SummaryID 
											CROSS APPLY (
															SELECT  TOP 1 IsHazmat, WillCall
															FROM	Quoting_SWEDetails SD
															WHERE	SD.QuoteNo = CSAF.SummaryID 
															AND		SD.CustID = CSAF.CustID
															AND		SD.WillCall = 'N'
														) Y
											WHERE	CSAF.CustID = @CustID 
											AND		CSAF.SummaryID = @SummaryID
											AND		CSAF.HZMTDestDelivFee = 0.00
											AND		CSAF.DestDelivFee = 0.00


											--Apply Will Call Fee - No Dest Deliv Applied when shipment is WC
											UPDATE			CSAF 
											SET				WillCallFee = ISNULL(
																					CASE WHEN DestDelivWC <= ISNULL(DestDelivWCMin,0.00) THEN ISNULL(DestDelivWCMin,0.00)
																							WHEN DestDelivWC > ISNULL(DestDelivWCMin,0.00) THEN DestDelivWC
																							END
																			,0.00), 
															HZMTDestDelivFee = CASE IsHazmat WHEN 'Y' THEN DestDelivHZMT ELSE 0.00 END
											FROM		Quoting_SWESummary csaf
											INNER JOIN  #DeliveryRate X ON X.CustID = CSAF.CustID AND X.SummaryID = CSAF.SummaryID 
											CROSS APPLY (
															SELECT  TOP 1 IsHazmat, WillCall
															FROM	Quoting_SWEDetails SD
															WHERE	SD.QuoteNo = CSAF.SummaryID 
															AND		SD.CustID = CSAF.CustID
															AND		SD.WillCall = 'Y'
														) Y
											WHERE	CSAF.CustID = @CustID 
											AND		CSAF.SummaryID = @SummaryID
											AND		CSAF.HZMTDestDelivFee = 0.00
											AND		CSAF.DestDelivFee = 0.00

			END


/**********************************************************************************INVASIVE SPECIES CHARGE**********************************************************************************/

			BEGIN


								UPDATE		CSAF
								SET			TotInvasiveSpeciesFee = ISNULL(CEILING((TotWeight + 1000) / 1000) * FlatFee,0.00)
								FROM		Quoting_SWESummary CSAF
								INNER JOIN  Quoting_Fees F ON F.CustID = CSAF.CustID AND State = 'HI'
								WHERE		CSAF.custid = @CustID 
								AND			CSAF.SummaryID = @SummaryID
								AND			CSAF.TotInvasiveSpeciesFee = 0.00
								AND			F.FeeTypeID = 3
								AND			F.FeeSubTypeID = 6
								AND			CAST(GETDATE() AS DATE) >= F.Effective 
								AND			CAST(GETDATE() AS DATE) < COALESCE(F.Terminated, GETDATE())


			END


/**********************************************************************************WHARFAGE CHARGE**********************************************************************************/

			BEGIN

								UPDATE		CSAF
								SET			TotWharfageFee = COALESCE(F.CalcFee,F1.CalcFee,0.00)
								FROM		Quoting_SWESummary CSAF
								OUTER APPLY  (
												SELECT ISNULL(FlatFee * CASE WHEN CONVERT(FLOAT,CONVERT(FLOAT,TotWeight) / CONVERT(FLOAT,TotCubicFeet)) > 45.00 THEN CONVERT(FLOAT,TotWeight) / 45.00
																			       ELSE TotCubicFeet 
																				   END --45LB Rule
															  ,0.00) CalcFee, F.Effective, F.Terminated
											    FROM	Quoting_Fees F 
												WHERE   F.CustID = CSAF.CustID 
												AND		F.State = 'HI'
												AND		F.FeeTypeID = 4
												AND		F.FeeSubTypeID = 5
												AND		F.FeeMax IS NULL
											) F
								OUTER APPLY  (
												SELECT CASE WHEN ISNULL(FlatFee * CASE WHEN CONVERT(FLOAT,CONVERT(FLOAT,TotWeight) / CONVERT(FLOAT,TotCubicFeet)) > 45.00 THEN CONVERT(FLOAT,TotWeight) / 45.00
																			       ELSE TotCubicFeet 
																				   END --45LB Rule
																,0.00) >= F.FeeMax 
															THEN F.FeeMax 
															ELSE ISNULL(FlatFee * CASE WHEN CONVERT(FLOAT,CONVERT(FLOAT,TotWeight) / CONVERT(FLOAT,TotCubicFeet)) > 45.00 THEN CONVERT(FLOAT,TotWeight) / 45.00
																			       ELSE TotCubicFeet 
																				   END --45LB Rule
																,0.00)
															END CalcFee, F.Effective, F.Terminated
											    FROM	Quoting_Fees F 
												WHERE   F.CustID = CSAF.CustID 
												AND		F.State = 'HI'
												AND		F.FeeTypeID = 4
												AND		F.FeeSubTypeID = 5
												AND		F.FeeMax IS NOT NULL
											) F1
								WHERE		CSAF.custid = @CustID
								AND			CSAF.SummaryID = @SummaryID
								AND			CSAF.TotWharfageFee = 0.00
								AND			CAST(GETDATE() AS DATE) >= COALESCE(F.Effective, F1.Effective)
								AND			CAST(GETDATE() AS DATE) < COALESCE(F.Terminated, F1.Terminated, GETDATE())

			END


/**********************************************************************************ACCESSORIAL CHARGE**********************************************************************************/

			BEGIN

			
								--Accessorial: HAZMAT Ocean Fee (Per Cubic Feet)
								UPDATE			CSAF 
								SET				HazmatOceanFee = ISNULL(
																			CASE WHEN TotCubicFeet * FlatFee <= FeeMin AND FeeMin IS NOT NULL AND FeeCalcTypeID = 1 THEN FeeMin 
																				 WHEN TotCubicFeet * FlatFee > FeeMin AND FeeMin IS NOT NULL AND FeeMax IS NULL AND FeeCalcTypeID = 1 THEN TotCubicFeet * FlatFee 
																				 WHEN TotCubicFeet * FlatFee > FeeMin AND FeeMin IS NOT NULL AND FeeMax IS NOT NULL AND TotCubicFeet * FlatFee > FeeMax AND FeeCalcTypeID = 1 THEN FeeMax 
																				 WHEN FeeCalcTypeID = 2 THEN F.FlatFee
																				 END 
																  ,0.00)
								FROM			Quoting_SWESummary CSAF
								CROSS APPLY		(
													SELECT	TOP 1 IsHazmat
													FROM	Quoting_SWEDetails SD
													WHERE	SD.QuoteNo = CSAF.SummaryID 
													AND		SD.CustID = CSAF.CustID
												) X
								INNER JOIN		Quoting_Fees F ON F.CustID = CSAF.CustID AND State = 'HI'
								WHERE	CSAF.custid = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.HazmatOceanFee = 0.00
								AND		X.IsHazmat = 'Y'
								AND		F.FeeTypeID = 1
								AND		F.FeeSubTypeID = 1
								AND		CAST(GETDATE() AS DATE) >= F.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(F.Terminated, GETDATE())
		 

								--Accessorial: Lift Gate Fee
								UPDATE		CSAF 
								SET			LiftGateFee = ISNULL(
																	CASE WHEN TotCubicFeet <= CuFtThreshold AND CuFtThreshold IS NOT NULL AND FeeCalcTypeID = 1 THEN FlatFee 
																		 WHEN CuFtThreshold IS NULL AND FeeCalcTypeID = 1 THEN FlatFee 
																		 ELSE FeeMax END 
														   ,0.00)
								FROM		Quoting_SWESummary CSAF
								CROSS APPLY (
												SELECT	TOP 1 LiftGate 
												FROM	Quoting_SWEDetails SD
												WHERE	SD.QuoteNo = CSAF.SummaryID 
												and		SD.CustID = CSAF.CustID
											) X
								INNER JOIN  Quoting_Fees F ON F.CustID = CSAF.CustID AND State = 'HI'
								WHERE	CSAF.custid = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.LiftGateFee = 0.00
								AND		X.LiftGate = 'Y'
								AND		F.FeeTypeID = 1
								AND		F.FeeSubTypeID = 2
								AND		CAST(GETDATE() AS DATE) >= F.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(F.Terminated, GETDATE())


								--Accessorial: Inside Delivery Fee
								UPDATE		CSAF 
								SET			InsideDeliveryFee = ISNULL(
																			CASE WHEN TotCubicFeet * F.FlatFee <= FeeMin AND FeeMin IS NOT NULL AND FeeCalcTypeID = 1 THEN FeeMin 
																				 WHEN TotCubicFeet * F.FlatFee > FeeMin AND FeeCalcTypeID = 1 THEN TotCubicFeet * F.FlatFee
																				 WHEN FeeMin IS NULL AND FeeCalcTypeID = 1 THEN TotCubicFeet * F.FlatFee
																				 WHEN FeeCalcTypeID = 2 THEN COALESCE(Y.FlatFee, F.FlatFee)
																				 END
																 ,0.00)
								FROM		Quoting_SWESummary CSAF
								CROSS APPLY (
												SELECT	TOP 1 InsideDelivery  
												FROM	Quoting_SWEDetails SD
												WHERE	SD.QuoteNo = CSAF.SummaryID 
												and		SD.CustID = CSAF.CustID
											) X
								INNER JOIN  Quoting_Fees F ON F.CustID = CSAF.CustID AND State = 'HI'
								OUTER APPLY (
												SELECT	FlatFee
												FROM	Quoting_InsideDeliveryRange IDR
												WHERE	IDR.FeeID = F.FeeID
												AND		CSAF.TotCubicFeet BETWEEN IDR.RangeMin AND IDR.RangeMax 
											) Y
								WHERE	CSAF.custid = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.InsideDeliveryFee = 0.00
								AND		X.InsideDelivery = 'Y'
								AND		F.FeeTypeID = 1
								AND		F.FeeSubTypeID = 3
								AND		CAST(GETDATE() AS DATE) >= F.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(F.Terminated, GETDATE())


								--Accessorial: Limited Access Fee
								UPDATE		CSAF 
								SET			LimitedAccessFee = ISNULL(
																		CASE WHEN TotCubicFeet * FlatFee <= FeeMin AND FeeMin IS NOT NULL AND FeeCalcTypeID = 1 THEN FeeMin 
																			 WHEN TotCubicFeet * FlatFee > FeeMin AND FeeCalcTypeID = 1 THEN TotCubicFeet * FlatFee
																			 WHEN FeeMin IS NULL AND FeeCalcTypeID = 1 THEN TotCubicFeet * FlatFee
																			 WHEN FeeCalcTypeID = 2 THEN FlatFee 
																			 END
																,0.00)
								from		Quoting_SWESummary CSAF
								CROSS APPLY (
												SELECT	TOP 1 LimitedAccess  
												FROM	Quoting_SWEDetails SD
												WHERE	SD.QuoteNo = CSAF.SummaryID 
												and		SD.CustID = CSAF.CustID
											) X
								INNER JOIN  Quoting_Fees F ON F.CustID = CSAF.CustID AND State = 'HI'
								WHERE	CSAF.custid = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.LimitedAccessFee = 0.00
								AND		X.LimitedAccess = 'Y'
								AND		F.FeeTypeID = 1
								AND		F.FeeSubTypeID = 4
								AND		CAST(GETDATE() AS DATE) >= F.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(F.Terminated, GETDATE())

/*
								--Accessorial: Hazmat Delivery Fee (Per Island)
								UPDATE		CSAF 
								SET			HazmatDeliveryFee = ISNULL(Y.AccHZMTDelivFee ,0.00)
								FROM		Quoting_SWESummary CSAF
								CROSS APPLY (
												SELECT	TOP 1 IsHazmat, WillCall, DestZipCode
												FROM	Quoting_SWEDetails SD
												WHERE	SD.QuoteNo = CSAF.SummaryID 
												AND		SD.CustID = CSAF.CustID
											) X
								CROSS APPLY (
												SELECT	TOP 1 AccHZMTDelivFee, Effective, Terminated
												FROM	Quoting_HiOceanRatesandDiscounts QH 
												WHERE	QH.CustID = CSAF.CustID
												AND		QH.ZipCode =  X.DestZipCode 
											) Y
								WHERE	CSAF.CustID = @CustID  
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.HazmatDeliveryFee = 0.00
								AND		X.IsHazmat = 'Y'
								AND		X.WillCall = 'N'
								AND		CAST(GETDATE() AS DATE) >= Y.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(Y.Terminated, GETDATE())

*/

								--Accessorial: Residential Delivery Fee
								UPDATE			CSAF 
								SET				ResidentialDeliveryFee = ISNULL(
																					CASE WHEN TotCubicFeet * FlatFee <= FeeMin AND FeeMin IS NOT NULL AND FeeCalcTypeID = 1 THEN FeeMin 
																						 WHEN TotCubicFeet * FlatFee > FeeMin AND FeeCalcTypeID = 1 THEN TotCubicFeet * FlatFee
																						 WHEN FeeMin IS NULL AND FeeCalcTypeID = 1 THEN TotCubicFeet * FlatFee
																						 WHEN FeeCalcTypeID = 2 THEN FlatFee 
																						 END
																		  ,0.00)
								FROM			Quoting_SWESummary CSAF
								CROSS APPLY		(
													SELECT	TOP 1 ResidentialDelivery
													FROM	Quoting_SWEDetails SD
													WHERE	SD.QuoteNo = CSAF.SummaryID 
													AND		SD.CustID = CSAF.CustID
												) X
								INNER JOIN		Quoting_Fees F ON F.CustID = CSAF.CustID AND State = 'HI'
								WHERE	CSAF.CustID = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.ResidentialDeliveryFee = 0.00
								AND		X.ResidentialDelivery = 'Y'
								AND		F.FeeTypeID = 1
								AND		F.FeeSubTypeID = 11
								AND		CAST(GETDATE() AS DATE) >= F.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(F.Terminated, GETDATE())


								--Appointment Fee
								UPDATE		CSAF 
								SET			AppointmentFee = ISNULL(F.FlatFee,0.00)
								FROM		Quoting_SWESummary CSAF
								CROSS APPLY		(
													SELECT	TOP 1 Appointment
													FROM	Quoting_SWEDetails SD
													WHERE	SD.QuoteNo = CSAF.SummaryID 
													AND		SD.CustID = CSAF.CustID
												) X
								INNER JOIN  Quoting_Fees F ON F.CustID = CSAF.CustID AND State = 'HI'
								WHERE	CSAF.custid = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.AppointmentFee = 0.00
								AND		X.Appointment = 'Y'
								AND		F.FeeTypeID = 1
								AND		F.FeeSubTypeID = 7
								AND		CAST(GETDATE() AS DATE) >= F.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(F.Terminated, GETDATE())


								--Excessive Length Fee
								UPDATE		CSAF 
								SET			ExcessiveLengthFee = ISNULL(Y.TotPcs * F.FlatFee,0.00)
								FROM		Quoting_SWESummary CSAF
								CROSS APPLY		(
													SELECT	TOP 1 ExcessiveLength
													FROM	Quoting_SWEDetails SD
													WHERE	SD.QuoteNo = CSAF.SummaryID 
													AND		SD.CustID = CSAF.CustID
												) X
								CROSS APPLY		(
													SELECT	COUNT(*) TotPcs
													FROM	Quoting_SWEDetails SD
													WHERE	SD.QuoteNo = CSAF.SummaryID 
													AND		SD.CustID = CSAF.CustID
													AND		Length >= 96 --8 feet
												) Y
								INNER JOIN  Quoting_Fees F ON F.CustID = CSAF.CustID AND State = 'HI'
								WHERE	CSAF.custid = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.ExcessiveLengthFee = 0.00
								AND		X.ExcessiveLength = 'Y'
								AND		F.FeeTypeID = 1
								AND		F.FeeSubTypeID = 9
								AND		CAST(GETDATE() AS DATE) >= F.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(F.Terminated, GETDATE())


								--Tradeshow Delivery Fee
								UPDATE		CSAF 
								SET			TradeshowDeliveryFee = ISNULL(F.FlatFee,0.00)
								FROM		Quoting_SWESummary CSAF
								CROSS APPLY		(
													SELECT	TOP 1 TradeshowDelivery
													FROM	Quoting_SWEDetails SD
													WHERE	SD.QuoteNo = CSAF.SummaryID 
													AND		SD.CustID = CSAF.CustID
												) X
								INNER JOIN  Quoting_Fees F ON F.CustID = CSAF.CustID AND State = 'HI'
								WHERE	CSAF.custid = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.TradeshowDeliveryFee = 0.00
								AND		X.TradeshowDelivery = 'Y'
								AND		F.FeeTypeID = 1
								AND		F.FeeSubTypeID = 12
								AND		CAST(GETDATE() AS DATE) >= F.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(F.Terminated, GETDATE())


								--Collect on Delivery Fee
								UPDATE		CSAF 
								SET			CollectOnDeliveryFee = ISNULL(
																			CASE WHEN F.Perc * X.CODAmountToCollect <= F.FeeMin AND FeeCalcTypeID = 1 THEN F.FeeMin
																				 WHEN F.Perc * X.CODAmountToCollect > F.FeeMin AND F.Perc * X.CODAmountToCollect <= F.FeeMax AND F.FeeCalcTypeID = 1 THEN F.Perc * X.CODAmountToCollect
																				 WHEN F.FeeCalcTypeID = 2 THEN F.FlatFee 
																				 END
																	,0.00)
								FROM		Quoting_SWESummary CSAF
								CROSS APPLY		(
													SELECT	TOP 1 CollectOnDelivery, CODAmountToCollect
													FROM	Quoting_SWEDetails SD
													WHERE	SD.QuoteNo = CSAF.SummaryID 
													AND		SD.CustID = CSAF.CustID
												) X
								INNER JOIN  Quoting_Fees F ON F.CustID = CSAF.CustID AND State = 'HI'
								WHERE	CSAF.custid = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.CollectOnDeliveryFee = 0.00
								AND		X.CollectOnDelivery = 'Y'
								AND		F.FeeTypeID = 1
								AND		F.FeeSubTypeID = 8
								AND		CAST(GETDATE() AS DATE) >= F.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(F.Terminated, GETDATE())


								--Accessorial: Inter Island Transfer Fee - APPLIES TO ALL CUSTOMERS ON QUOTES TO LANAI AND MOLOKAI ONLY
								UPDATE			CSAF 
								SET				InterIslandTransFee = ISNULL(
																				CASE WHEN TotCubicFeet * FlatFee <= FeeMin  THEN FeeMin 
																					 ELSE TotCubicFeet * FlatFee 
																					 END
																	    ,0.00)
								FROM			Quoting_SWESummary CSAF
								CROSS APPLY		(
													SELECT TOP 1 HORAD.InterIslandFee FlatFee, InterIslandFeeMin FeeMin
													FROM		Quoting_SWEDetails SD 
													INNER JOIN  Quoting_HiOceanRatesandDiscounts HORAD ON HORAD.CustID = SD.CustID AND HORAD.ZipCode = SD.DestZipCode AND HORAD.Island IN ('LANAI','MOLOKAI')
													WHERE		SD.CustID = CSAF.CustID
													AND			SD.QuoteNo = CSAF.SummaryID
													AND			CAST(GETDATE() AS DATE) >= HORAD.Effective 
													AND			CAST(GETDATE() AS DATE) < COALESCE(HORAD.Terminated, GETDATE())
												) X
								WHERE	CSAF.CustID = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.InterIslandTransFee = 0.00


								--Accessorial: Notification Fee
								UPDATE			CSAF 
								SET				NotificationFee = ISNULL(
																			CASE WHEN TotCubicFeet * FlatFee <= FeeMin AND FeeMin IS NOT NULL AND FeeCalcTypeID = 1 THEN FeeMin 
																				 WHEN TotCubicFeet * FlatFee > FeeMin AND FeeCalcTypeID = 1 THEN TotCubicFeet * FlatFee
																				 WHEN FeeMin IS NULL AND FeeCalcTypeID = 1 THEN TotCubicFeet * FlatFee
																				 WHEN FeeCalcTypeID = 2 THEN FlatFee 
																				 END
																   ,0.00)
								FROM			Quoting_SWESummary CSAF
								CROSS APPLY		(
													SELECT	TOP 1 Notification
													FROM	Quoting_SWEDetails SD
													WHERE	SD.QuoteNo = CSAF.SummaryID 
													AND		SD.CustID = CSAF.CustID
												) X
								INNER JOIN		Quoting_Fees F ON F.CustID = CSAF.CustID AND State = 'HI'
								WHERE	CSAF.CustID = @CustID 
								AND		CSAF.SummaryID = @SummaryID
								AND		CSAF.NotificationFee = 0.00
								AND		X.Notification = 'Y'
								AND		F.FeeTypeID = 1
								AND		F.FeeSubTypeID = 20
								AND		CAST(GETDATE() AS DATE) >= F.Effective 
								AND		CAST(GETDATE() AS DATE) < COALESCE(F.Terminated, GETDATE())

							    --Update Total Cubic Feet to incorporate 45lb Rule. This is for display reasons only on the quote results
								UPDATE		CSAF 
								SET			[TotCubicFeet] = [45lbCuFt] + ISNULL([CuFt],0)
								FROM		Quoting_SWESummary CSAF 
								CROSS APPLY (
												SELECT   QuoteNo, SUM(ROUND(CONVERT(FLOAT,WeightPerPKGType) / 45.00,0)) [45lbCuFt]
												FROM	 Quoting_SWEDetails SD
												WHERE	 SD.CustID = CSAF.CustID
												AND		 SD.QuoteNo = CSAF.SummaryID
												AND		 CONVERT(FLOAT,CONVERT(FLOAT,WeightPerPKGType) / CONVERT(FLOAT,SD.CubicFeet)) > 45.00
												GROUP BY QuoteNo
											) SD
								OUTER APPLY (
												SELECT  QuoteNo, ISNULL(SUM(CubicFeet),0.00) [CuFt]
												FROM	Quoting_SWEDetails SD
												WHERE	SD.CustID = CSAF.CustID
												AND		SD.QuoteNo = CSAF.SummaryID
												AND		CONVERT(FLOAT,CONVERT(FLOAT,WeightPerPKGType) / CONVERT(FLOAT,SD.CubicFeet)) <= 45.00
												GROUP BY QuoteNo
											) SD1
								WHERE		CSAF.CustID = @CustID
								AND			CSAF.SummaryID = @SummaryID

								UPDATE  CSAF 
								SET		TotAccessorialFee =		ISNULL(HazmatOceanFee,0.00) 
															+	ISNULL(HazmatDeliveryFee,0.00) 
															+	ISNULL(LiftGateFee,0.00) 
															+	ISNULL(InsideDeliveryFee,0.00) 
															+	ISNULL(ResidentialDeliveryFee,0.00)
															+	ISNULL(LimitedAccessFee,0.00)
															+	ISNULL(AppointmentFee,0.00)
															+	ISNULL(CollectOnDeliveryFee,0.00)
															+	ISNULL(ExcessiveLengthFee,0.00)
															+	ISNULL(TradeshowDeliveryFee,0.00)
															+   ISNULL(InterIslandTransFee,0.00)
															+   ISNULL(NotificationFee,0.00)

								FROM	Quoting_SWESummary CSAF
								WHERE	CSAF.TotAccessorialFee = 0.00
								AND		CSAF.CustID = @CustID 
								AND		CSAF.SummaryID = @SummaryID
 

			END

END
End Try



BEGIN CATCH


              DECLARE @ErrorMessage NVARCHAR(4000), @ErrorLine INT, @ErrorDate DATETIME;

              
              SELECT @ErrorMessage = ERROR_MESSAGE(), @ErrorLine = ERROR_LINE(), @ErrorDate = GETDATE();
              
              Insert into AirTrak.dbo.tblProcedureErrorLog ([ProcedureName], [ErrorMessage], [ErrorLine], [ErrorDate])
              Select '[Quoting_CalcHIQuote]', @ErrorMessage, @ErrorLine, @ErrorDate



END CATCH
