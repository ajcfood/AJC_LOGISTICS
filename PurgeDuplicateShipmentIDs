USE [AJCDW]
GO
/****** Object:  StoredProcedure [dbo].[PurgeDuplicateShipmentIDs]    Script Date: 11/20/2019 9:22:58 AM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO



ALTER procedure [dbo].[PurgeDuplicateShipmentIDs] as
BEGIN TRY
BEGIN



					--Find Dup ShipmentIDs
					IF OBJECT_ID ('TEMPDB..#Dups') IS NOT NULL  DROP TABLE #Dups
					select ShipmentID, Count(*) TotalCount
					into #Dups
					from AJCDW.dbo.tblShipment 
					group by ShipmentID
					having count(*) > 1

					--Mark all records after first entry based on ShpRecID as dup
					IF OBJECT_ID ('TEMPDB..#Purge') IS NOT NULL  DROP TABLE #Purge
					select ROW_NUMBER() OVER (PARTITION BY ts.ShipmentID ORDER BY ts.ShpRecID) Row,ts.*
					into #Purge
					from AJCDW.dbo.tblShipment ts
					inner join #Dups t on t.ShipmentID = ts.ShipmentID

					DELETE 
					FROM	AJCDW.dbo.tblShipment
					OUTPUT			Deleted.[ShpRecID]
								  , Deleted.[ShipmentID]
								  , Deleted.[RefNo]
								  , Deleted.[ShpBkgNum]
								  , Deleted.[ShpCustPONo]
								  , Deleted.[ShpAccnt]
								  , Deleted.[ShpDivID]
								  , Deleted.[ShpLOB]
								  , Deleted.[ShpCarrier]
								  , Deleted.[ShpExporter]
								  , Deleted.[ShpConsg]
								  , Deleted.[ShpSalesP]
								  , Deleted.[ShpController]
								  , Deleted.[ShpBLNum]
								  , Deleted.[ShpMaster]
								  , Deleted.[ShpMoveType]
								  , Deleted.[ShpPier]
								  , Deleted.[ShpContainer]
								  , Deleted.[ShpOriginCity]
								  , Deleted.[ShpOriginState]
								  , Deleted.[ShpDelivCity]
								  , Deleted.[ShpDelivState]
								  , Deleted.[ShpPortofDchg]
								  , Deleted.[ShpPortofLoad]
								  , Deleted.[ShpVessel_Equipmnt]
								  , Deleted.[ShpPntOrigin]
								  , Deleted.[ShpAES_ITN]
								  , Deleted.[ShpAES_STATUS]
								  , Deleted.[ShpTEU]
								  , Deleted.[ShpREVENUE]
								  , Deleted.[ShpCOST]
								  , Deleted.[ShpFreightCost]
								  , Deleted.[ShpGP]
								  , Deleted.[ShpGPPerc]
								  , Deleted.[ShpFileOpenDate]
								  , Deleted.[ShpDelivDate]
								  , Deleted.[ShpShipDate]
								  , Deleted.[LASTUPDTDATE]
								  , Deleted.[ShpLoadedDate]
								  , Deleted.[ShpStatus]
								  , Deleted.[FK_SystemID]
								  , Deleted.[FK_DivID]
								  , Deleted.[FK_CustMasterID]
								  , Deleted.[FK_SalesPID]
								  , Deleted.[FK_ShipmentTypeID]
								  , Deleted.[FK_StationID]
								  , Deleted.[FK_VendID]
								  , Deleted.[FK_FileID]
								  , Deleted.[FK_ConsgID]
								  , Deleted.[FK_ShipperID]
								  , GETDATE()
					INTO		AJCDW.dbo.tblShipment_Deleted 
					FROM		AJCDW.dbo.tblShipment ts
					INNER JOIN	#Purge p on p.ShpRecID = ts.ShpRecID
					WHERE		p.Row > 1


END

End Try



BEGIN CATCH


              DECLARE @ErrorMessage NVARCHAR(4000), @ErrorLine INT, @ErrorDate DATETIME;

              
              SELECT @ErrorMessage = ERROR_MESSAGE(), @ErrorLine = ERROR_LINE(), @ErrorDate = GETDATE();
              
              Insert into AJCDW.dbo.tblProcedureErrorLog ([ProcedureName], [ErrorMessage], [ErrorLine], [ErrorDate])
              Select '[PurgeDuplicateShipmentIDs]', @ErrorMessage, @ErrorLine, @ErrorDate



END CATCH
