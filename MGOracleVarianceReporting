USE [AJCDW]
GO
/****** Object:  StoredProcedure [dbo].[MGOracleVarianceReporting]    Script Date: 1/29/2020 3:53:25 PM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO





ALTER procedure [dbo].[MGOracleVarianceReporting] as

BEGIN TRY
BEGIN

DECLARE @ProcName VARCHAR(100) = OBJECT_NAME(@@PROCID)

/*****************************************************Begin Logging*******************************************************/

 EXEC AJCDW.dbo.spAJCDW_Logging @StepID = 1, @BeginOrEnd = 0, @ProcName = @ProcName

 /*************************************************************************************************************************/

 
 /*****************************************************************LOADS WITH VARIANCES REPORT*****************************************************************/

					DECLARE @Message VARCHAR(MAX)
					
					INSERT INTO AJCDW.dbo.MGToOracleVariance (
																SO_Number, LD#, RouteCostFilename, RouteCostLoadedDateTime, RouteRevFilename, RouteRevLoadedDateTime, CarrierInvFilename, CarrierInvLoadedDateTime, CustInvFilename, 
																CustInvLoadedDateTime, RouteFileCost, CarrierInvTotal, OracleCost, RouteFileRev, CustomerInvTotal, OracleRev 
															 )
					SELECT	COALESCE(v.RefShippingOrder, w.RefShippingOrder, x.RefShippingOrder, y.RefShippingOrder, z.OracleRefShippingOrder) SO_Number, COALESCE(v.RefLoadID, w.RefLoadID, x.RefLoadID, y.RefLoadID) LD#,
						    v.FileName RouteCostFilename, v.LoadedDateTime RouteCostLoadedDateTime, 
							w.FileName RouteRevFilename, w.LoadedDateTime RouteRevLoadedDateTime, y.Filename CarrierInvFilename, y.LoadedDateTime CarrierInvLoadedDateTime, x.FileName CustInvFilename, x.LoadedDateTime CustInvLoadedDateTime, 
							RouteFileCost, CarrierInvTotal, OracleCost, RouteFileRev, CustomerInvTotal, OracleRev
					FROM 				(
												SELECT x.RefShippingOrder, x.RefLoadID, SUM(ChargesAmount) RouteFileRev, x.Filename, CAST(LoadedDateTime AS DATETIME) LoadedDateTime
												FROM   MGXMLProcessing.[dbo].[tblRouteXML_TransportLoadRecs] x
												INNER JOIN (
																SELECT RefShippingOrder, MAX(Filename) Filename
																FROM MGXMLProcessing.[dbo].[tblRouteXML_TransportLoadRecs]
																GROUP BY RefShippingOrder
															) y ON y.RefShippingOrder = x.RefShippingOrder AND y.Filename = x.FileName
												WHERE PriceSheetType = 'Cost'
												GROUP BY x.RefShippingOrder, x.RefLoadID, x.Filename, CAST(LoadedDateTime AS DATETIME) 
										) V
					FULL OUTER JOIN		(
												SELECT x.RefShippingOrder, x.RefLoadID, SUM(ChargesAmount) RouteFileCost, x.Filename, CAST(LoadedDateTime AS DATETIME) LoadedDateTime
												FROM   MGXMLProcessing.[dbo].[tblRouteXML_TransportLoadRecs] x
												INNER JOIN (
																SELECT RefShippingOrder, MAX(Filename) Filename
																FROM MGXMLProcessing.[dbo].[tblRouteXML_TransportLoadRecs]
																GROUP BY RefShippingOrder
															) y ON y.RefShippingOrder = x.RefShippingOrder AND y.Filename = x.FileName
												WHERE PriceSheetType in ('Charge','VendorRate')
												GROUP BY x.RefShippingOrder, x.RefLoadID, x.Filename, CAST(LoadedDateTime AS DATETIME) 
										) W ON W.RefShippingOrder = V.RefShippingOrder						
					FULL OUTER JOIN		(
												SELECT x.RefShippingOrder, x.RefLoadID, SUM(ChargesAmount) CustomerInvTotal, x.Filename, CAST(LoadedDateTime AS DATETIME) LoadedDateTime
												FROM   MGXMLProcessing.[dbo].[tblCustomerXML_InvoiceLoadRecs] x
												INNER JOIN (
																SELECT RefShippingOrder, MAX(Filename) Filename
																FROM MGXMLProcessing.[dbo].[tblCustomerXML_InvoiceLoadRecs]
																GROUP BY RefShippingOrder
														   ) y ON y.RefShippingOrder = x.RefShippingOrder AND y.Filename = x.FileName
												GROUP BY x.RefShippingOrder, x.RefLoadID, x.Filename, CAST(LoadedDateTime AS DATETIME) 
										) X ON X.RefShippingOrder = COALESCE(v.RefShippingOrder, w.RefShippingOrder)
					FULL OUTER JOIN		(
												SELECT x.RefShippingOrder, x.RefLoadID, SUM(ChargesAmount) CarrierInvTotal, x.Filename, CAST(LoadedDateTime AS DATETIME) LoadedDateTime
												FROM   MGXMLProcessing.[dbo].[tblCarrierXML_InvoiceLoadRecs] x
												INNER JOIN (
																SELECT RefShippingOrder, MAX(Filename) Filename
																FROM MGXMLProcessing.[dbo].[tblCarrierXML_InvoiceLoadRecs]
																GROUP BY RefShippingOrder
														   ) y ON y.RefShippingOrder = x.RefShippingOrder AND y.Filename = x.FileName
												GROUP BY x.RefShippingOrder, x.RefLoadID, x.Filename, CAST(LoadedDateTime AS DATETIME) 
										) Y ON y.RefShippingOrder = COALESCE(v.RefShippingOrder, w.RefShippingOrder)
					FULL OUTER JOIN		(
																
											SELECT  REPLACE(LINE_ATTRIBUTE11,'MG','') OracleRefShippingOrder, SUM(OREVENUE) OracleRev, SUM(OCOST) OracleCost
											FROM	ajcdw.[dbo].[tblOracleGLCleansed]
											WHERE   LOB IN ('10','11')
											GROUP BY REPLACE(LINE_ATTRIBUTE11,'MG',''), LOB 
										) Z ON Z.OracleRefShippingOrder = COALESCE(v.RefShippingOrder, w.RefShippingOrder)
					WHERE		(
									RouteFileCost <> ISNULL(CarrierInvTotal,RouteFileCost) 
								OR  RouteFileCost <> ISNULL(OracleCost,0.00)
								OR  RouteFileRev  <> ISNULL(CustomerInvTotal,RouteFileRev)
								OR  RouteFileRev  <> ISNULL(OracleRev,0.00)
								)
					AND			(
										v.LoadedDateTime >= DATEADD(dd,-1,CAST(GETDATE() AS DATE))
									 OR w.LoadedDateTime >= DATEADD(dd,-1,CAST(GETDATE() AS DATE))
									 OR x.LoadedDateTime >= DATEADD(dd,-1,CAST(GETDATE() AS DATE))
									 OR y.LoadedDateTime >= DATEADD(dd,-1,CAST(GETDATE() AS DATE))
								)
					
					IF EXISTS (SELECT TOP 1 1 FROM AJCDW.dbo.MGToOracleVariance WHERE CAST(DateAdded AS DATE) = CAST(GETDATE() AS DATE))
					BEGIN

								SET @Message =  
												N'
												<html>
												<body>
												<h3>Below LDs have variances between MG and BI or MG and Oracle. Please research.</h3>
												<table border="1">
												<tr><th>SO_Number</th>
												<th>LD#</th>
												<th>RouteCostFilename</th>
												<th>RouteCostLoadedDateTime</th>
												<th>RouteRevFilename</th>
												<th>RouteRevLoadedDateTime</th>
												<th>CarrierInvFilename</th>
												<th>CarrierInvLoadedDateTime</th>
												<th>CustInvFilename</th>
												<th>CustInvLoadedDateTime</th>
												<th>RouteFileCost</th>
												<th>CarrierInvTotal</th>
												<th>OracleCost</th>
												<th>RouteFileRev</th>
												<th>CustomerInvTotal</th>
												<th>OracleRev</th>
												</tr>' +  
															CAST ( ( SELECT td = ISNULL(SO_Number,''), '',
																			td = ISNULL(LD#,''), '',
																			td = ISNULL(RouteCostFilename,''), '',  
																			td = ISNULL(CONVERT(VARCHAR,RouteCostLoadedDateTime,0),''), '', 
																			td = ISNULL(RouteRevFilename,''), '', 
																			td = ISNULL(CONVERT(VARCHAR,RouteRevLoadedDateTime, 0),''), '',  
																			td = ISNULL(CarrierInvFilename,''), '',  
																			td = ISNULL(CONVERT(VARCHAR,CarrierInvLoadedDateTime, 0),''), '', 
																			td = ISNULL(CustInvFilename,''), '', 
																			td = ISNULL(CONVERT(VARCHAR,CustInvLoadedDateTime, 0),''), '', 
																			td = ISNULL(RouteFileCost,0.00), '',
																			td = ISNULL(CarrierInvTotal,0.00), '',
																			td = ISNULL(OracleCost,0.00), '',
																			td = ISNULL(RouteFileRev,0.00), '',
																			td = ISNULL(CustomerInvTotal,0.00), '',
																			td = ISNULL(OracleRev,0.00), ''
																		FROM AJCDW.dbo.MGToOracleVariance
																		WHERE CAST(DateAdded AS DATE) = CAST(GETDATE() AS DATE)  
																		FOR XML PATH('tr'), TYPE   
															) AS NVARCHAR(MAX) ) + 
			
												N'</table>
												</body>
												</html>'


									EXEC msdb.dbo.sp_send_dbmail 
										@profile_name = 'APTEST01SQLMail'  
									,@recipients = 'sali@ajclogistics.com;seljamil@ajclogistics.com' 
									,@body = @Message
									,@body_format = 'HTML'
									,@subject = 'Daily MG-BI-Oracle Variance Report'
									,@Importance = 'HIGH';  

					END

					ELSE
					BEGIN

					SET @Message =  
												N'
												<html>
												<body>
												<h3>There were no loads with variances in the MercuryGate integration today.</h3>
												</body>
												</html>'


									EXEC msdb.dbo.sp_send_dbmail 
										@profile_name = 'APTEST01SQLMail'  
									,@recipients = 'sali@ajclogistics.com;seljamil@ajclogistics.com' 
									,@body = @Message
									,@body_format = 'HTML'
									,@subject = 'Daily MG-BI-Oracle Variance Report'
									,@Importance = 'HIGH';  
					
					END


/*****************************************************End Logging*******************************************************/

 EXEC AJCDW.dbo.spAJCDW_Logging @StepID = 1, @BeginOrEnd = 1, @RecordsProcessed = @@ROWCOUNT, @ProcName = @ProcName

 /***********************************************************************************************************************/
 /*****************************************************Begin Logging*******************************************************/

 EXEC AJCDW.dbo.spAJCDW_Logging @StepID = 2, @BeginOrEnd = 0, @ProcName = @ProcName

 /*************************************************************************************************************************/



  /*****************************************************************HISTORICAL VARIANCE REPORT*****************************************************************/
DECLARE @PrevVariances TABLE (LD# VARCHAR(10), SO_Number VARCHAR(10))

--Pull all loads with variances from 7 days ago
INSERT INTO @PrevVariances (SO_Number)
SELECT SO_Number
FROM AJCDW.dbo.MGToOracleVariance
WHERE CAST(DateAdded AS DATE) <= DATEADD(dd,-7,CAST(GETDATE() AS DATE))
 
--Run report on loads with variances reported 7 days ago to see if variance still exists. Else delete output the loads to AJCDW.dbo.MGToOracleVarianceDeleted
SELECT	COALESCE(v.RefShippingOrder, w.RefShippingOrder, x.RefShippingOrder, y.RefShippingOrder, z.OracleRefShippingOrder) SO_Number, COALESCE(v.RefLoadID, w.RefLoadID, x.RefLoadID, y.RefLoadID) LD#,
		v.FileName RouteCostFilename, v.LoadedDateTime RouteCostLoadedDateTime, 
		w.FileName RouteRevFilename, w.LoadedDateTime RouteRevLoadedDateTime, y.Filename CarrierInvFilename, y.LoadedDateTime CarrierInvLoadedDateTime, x.FileName CustInvFilename, x.LoadedDateTime CustInvLoadedDateTime, 
		RouteFileCost, CarrierInvTotal, OracleCost, RouteFileRev, CustomerInvTotal, OracleRev
INTO	#TMP
FROM 				(
							SELECT x.RefShippingOrder, x.RefLoadID, SUM(ChargesAmount) RouteFileRev, x.Filename, CAST(LoadedDateTime AS DATETIME) LoadedDateTime
							FROM   MGXMLProcessing.[dbo].[tblRouteXML_TransportLoadRecs] x
							INNER JOIN (
											SELECT RefShippingOrder, MAX(Filename) Filename
											FROM MGXMLProcessing.[dbo].[tblRouteXML_TransportLoadRecs]
											GROUP BY RefShippingOrder
										) y ON y.RefShippingOrder = x.RefShippingOrder AND y.Filename = x.FileName
							WHERE PriceSheetType = 'Cost'
							AND   x.RefShippingOrder IN (SELECT SO_Number FROM @PrevVariances)
							GROUP BY x.RefShippingOrder, x.RefLoadID, x.Filename, CAST(LoadedDateTime AS DATETIME) 
					) V
FULL OUTER JOIN		(
							SELECT x.RefShippingOrder, x.RefLoadID, SUM(ChargesAmount) RouteFileCost, x.Filename, CAST(LoadedDateTime AS DATETIME) LoadedDateTime
							FROM   MGXMLProcessing.[dbo].[tblRouteXML_TransportLoadRecs] x
							INNER JOIN (
											SELECT RefShippingOrder, MAX(Filename) Filename
											FROM MGXMLProcessing.[dbo].[tblRouteXML_TransportLoadRecs]
											GROUP BY RefShippingOrder
										) y ON y.RefShippingOrder = x.RefShippingOrder AND y.Filename = x.FileName
							WHERE PriceSheetType in ('Charge','VendorRate')
							AND   x.RefShippingOrder IN (SELECT SO_Number FROM @PrevVariances)
							GROUP BY x.RefShippingOrder, x.RefLoadID, x.Filename, CAST(LoadedDateTime AS DATETIME) 
					) W ON W.RefShippingOrder = V.RefShippingOrder						
FULL OUTER JOIN		(
							SELECT x.RefShippingOrder, x.RefLoadID, SUM(ChargesAmount) CustomerInvTotal, x.Filename, CAST(LoadedDateTime AS DATETIME) LoadedDateTime
							FROM   MGXMLProcessing.[dbo].[tblCustomerXML_InvoiceLoadRecs] x
							INNER JOIN (
											SELECT RefShippingOrder, MAX(Filename) Filename
											FROM MGXMLProcessing.[dbo].[tblCustomerXML_InvoiceLoadRecs]
											GROUP BY RefShippingOrder
										) y ON y.RefShippingOrder = x.RefShippingOrder AND y.Filename = x.FileName
							WHERE x.RefShippingOrder IN (SELECT SO_Number FROM @PrevVariances)
							GROUP BY x.RefShippingOrder, x.RefLoadID, x.Filename, CAST(LoadedDateTime AS DATETIME) 
					) X ON X.RefShippingOrder = COALESCE(v.RefShippingOrder, w.RefShippingOrder)
FULL OUTER JOIN		(
							SELECT x.RefShippingOrder, x.RefLoadID, SUM(ChargesAmount) CarrierInvTotal, x.Filename, CAST(LoadedDateTime AS DATETIME) LoadedDateTime
							FROM   MGXMLProcessing.[dbo].[tblCarrierXML_InvoiceLoadRecs] x
							INNER JOIN (
											SELECT RefShippingOrder, MAX(Filename) Filename
											FROM MGXMLProcessing.[dbo].[tblCarrierXML_InvoiceLoadRecs]
											GROUP BY RefShippingOrder
										) y ON y.RefShippingOrder = x.RefShippingOrder AND y.Filename = x.FileName
							WHERE x.RefShippingOrder IN (SELECT SO_Number FROM @PrevVariances)
							GROUP BY x.RefShippingOrder, x.RefLoadID, x.Filename, CAST(LoadedDateTime AS DATETIME) 
					) Y ON y.RefShippingOrder = COALESCE(v.RefShippingOrder, w.RefShippingOrder)
FULL OUTER JOIN		(
																
						SELECT  REPLACE(LINE_ATTRIBUTE11,'MG','') OracleRefShippingOrder, SUM(OREVENUE) OracleRev, SUM(OCOST) OracleCost
						FROM	ajcdw.[dbo].[tblOracleGLCleansed]
						WHERE   LOB IN ('10','11')
						AND		REPLACE(LINE_ATTRIBUTE11,'MG','') IN (SELECT SO_Number FROM @PrevVariances)
						GROUP BY REPLACE(LINE_ATTRIBUTE11,'MG',''), LOB 
					) Z ON Z.OracleRefShippingOrder = COALESCE(v.RefShippingOrder, w.RefShippingOrder)
WHERE		(
				RouteFileCost <> ISNULL(CarrierInvTotal,RouteFileCost) 
			OR  RouteFileCost <> ISNULL(OracleCost,0.00)
			OR  RouteFileRev  <> ISNULL(CustomerInvTotal,RouteFileRev)
			OR  RouteFileRev  <> ISNULL(OracleRev,0.00)
			)

					
IF EXISTS (SELECT TOP 1 1 FROM #TMP)
BEGIN

			SET @Message =  
							N'
							<html>
							<body>
							<h3>Below LDs have previously been reported with a variance. The variances have existed now for 7 days or more. Please review and correct these variance immediately.</h3>
							<table border="1">
							<tr><th>SO_Number</th>
							<th>LD#</th>
							<th>RouteCostFilename</th>
							<th>RouteCostLoadedDateTime</th>
							<th>RouteRevFilename</th>
							<th>RouteRevLoadedDateTime</th>
							<th>CarrierInvFilename</th>
							<th>CarrierInvLoadedDateTime</th>
							<th>CustInvFilename</th>
							<th>CustInvLoadedDateTime</th>
							<th>RouteFileCost</th>
							<th>CarrierInvTotal</th>
							<th>OracleCost</th>
							<th>RouteFileRev</th>
							<th>CustomerInvTotal</th>
							<th>OracleRev</th>
							</tr>' +  
										CAST ( ( SELECT td = ISNULL(SO_Number,''), '',
														td = ISNULL(LD#,''), '',
														td = ISNULL(RouteCostFilename,''), '',  
														td = ISNULL(CONVERT(VARCHAR,RouteCostLoadedDateTime,0),''), '', 
														td = ISNULL(RouteRevFilename,''), '', 
														td = ISNULL(CONVERT(VARCHAR,RouteRevLoadedDateTime, 0),''), '',  
														td = ISNULL(CarrierInvFilename,''), '',  
														td = ISNULL(CONVERT(VARCHAR,CarrierInvLoadedDateTime, 0),''), '', 
														td = ISNULL(CustInvFilename,''), '', 
														td = ISNULL(CONVERT(VARCHAR,CustInvLoadedDateTime, 0),''), '', 
														td = ISNULL(RouteFileCost,0.00), '',
														td = ISNULL(CarrierInvTotal,0.00), '',
														td = ISNULL(OracleCost,0.00), '',
														td = ISNULL(RouteFileRev,0.00), '',
														td = ISNULL(CustomerInvTotal,0.00), '',
														td = ISNULL(OracleRev,0.00), ''
													FROM #TMP
													FOR XML PATH('tr'), TYPE   
										) AS NVARCHAR(MAX) ) + 
			
							N'</table>
							</body>
							</html>'


				EXEC msdb.dbo.sp_send_dbmail 
					@profile_name = 'APTEST01SQLMail'  
				,@recipients = 'sali@ajclogistics.com;seljamil@ajclogistics.com' 
				,@body = @Message
				,@body_format = 'HTML'
				,@subject = 'MG-BI-Oracle Variance for 7 Days or More'
				,@Importance = 'HIGH';  

END

ELSE
BEGIN

SET @Message =  
							N'
							<html>
							<body>
							<h3>There are no loads that have had a variance for 7 days or more.</h3>
							</body>
							</html>'


				EXEC msdb.dbo.sp_send_dbmail 
					@profile_name = 'APTEST01SQLMail'  
				,@recipients = 'sali@ajclogistics.com;seljamil@ajclogistics.com' 
				,@body = @Message
				,@body_format = 'HTML'
				,@subject = 'MG-BI-Oracle Variance for 7 Days or More'
				,@Importance = 'HIGH';  
					
END

/*****************************************************End Logging*******************************************************/

 EXEC AJCDW.dbo.spAJCDW_Logging @StepID = 2, @BeginOrEnd = 1, @RecordsProcessed = @@ROWCOUNT, @ProcName = @ProcName

 /***********************************************************************************************************************/
  /*****************************************************Begin Logging*******************************************************/

 EXEC AJCDW.dbo.spAJCDW_Logging @StepID = 3, @BeginOrEnd = 0, @ProcName = @ProcName

 /*************************************************************************************************************************/

--Move loads that no longer have variances
DELETE  x
OUTPUT		deleted.[SO_Number]
	      , [LD#]
		  , [RouteCostFilename]
		  , [RouteCostLoadedDateTime]
		  , [RouteRevFilename]
		  , [RouteRevLoadedDateTime]
		  , [CarrierInvFilename]
		  , [CarrierInvLoadedDateTime]
		  , [CustInvFilename]
		  , [CustInvLoadedDateTime]
		  , [RouteFileCost]
		  , [CarrierInvTotal]
		  , [OracleCost]
		  , [RouteFileRev]
		  , [CustomerInvTotal]
		  , [OracleRev]
		  , [DateAdded]
		  , GETDATE() 
INTO	AJCDW.dbo.MGToOracleVarianceDeleted
FROM	AJCDW.dbo.MGToOracleVariance x
LEFT JOIN #TMP T on T.SO_Number = x.SO_Number
WHERE	CAST(x.DateAdded AS DATE) <= DATEADD(dd,-7,CAST(GETDATE() AS DATE))
AND		T.SO_Number IS NULL 


/*****************************************************End Logging*******************************************************/

 EXEC AJCDW.dbo.spAJCDW_Logging @StepID = 3, @BeginOrEnd = 1, @RecordsProcessed = @@ROWCOUNT, @ProcName = @ProcName

 /***********************************************************************************************************************/

END

END TRY



BEGIN CATCH


              DECLARE @ErrorMessage NVARCHAR(4000), @ErrorLine INT, @ErrorDate DATETIME;

              
              SELECT @ErrorMessage = ERROR_MESSAGE(), @ErrorLine = ERROR_LINE(), @ErrorDate = GETDATE();
              
              INSERT INTO AJCDW.dbo.tblProcedureErrorLog ([ProcedureName], [ErrorMessage], [ErrorLine], [ErrorDate])
              SELECT '[MGOracleVarianceReporting]', @ErrorMessage, @ErrorLine, @ErrorDate



END CATCH
